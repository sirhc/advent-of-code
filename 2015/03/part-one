#!/usr/bin/env perl

use strict;
use v5.34;

my @position = ( 0, 0 );
my %visits = ( '0,0' => 1 );  # don't forget to gift the first house
my %moves = (
  '^' => [  0,  1 ],  # x, y
  'v' => [  0, -1 ],
  '>' => [  1,  0 ],
  '<' => [ -1,  0 ],
);

while ( my $line = <> ) {
  chomp $line;

  for my $move ( split //, $line ) {
    printf '(%d, %d) %s ', @position, $move;

    @position = map { $position[$_] + $moves{$move}[$_] } 0, 1;
    $visits{ join ',', @position } += 1;

    printf '(%d, %d) #%d', @position, $visits{ join ',', @position };
    printf "\n";
  }
}

printf "\n";
printf "Houses visited: %d\n", scalar keys %visits;
